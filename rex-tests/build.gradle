plugins {
    id 'application'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.2'
    testImplementation 'net.sf.saxon:Saxon-HE:12.7', {
        exclude group: "xml-apis", module: "xml-apis"
    }
    testImplementation 'org.basex:basex:11.9'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

test {
    if (project.hasProperty('USE_VALGRIND')) {
        systemProperty 'USE_VALGRIND', project.property('USE_VALGRIND')
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\n" +
                "${result.testCount} tests completed: " +
                "${result.successfulTestCount} succeeded, " +
                "${result.skippedTestCount} skipped, " +
                "${result.failedTestCount} failed."
            def testSummary = """# Test Summary
|           | Count
|-----------|-------
|     Total | ${result.testCount}
| Succeeded | ${result.successfulTestCount}
|   Skipped | ${result.skippedTestCount}
|    Failed | ${result.failedTestCount}"""
            def testSummaryFile = file("${buildDir}/test-results/test-summary.md")
            testSummaryFile.text = testSummary
        }
    }
}
