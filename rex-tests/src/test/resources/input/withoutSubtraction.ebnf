X ::= 'A' CommentContentsE
    | 'B' CommentContents
    | 'C' PragmaContents
    | 'D' DirPIContents
    | 'E' CDataSectionContents
    | 'F' CDSect
    | 'G' CharData
    | 'H' PITarget
    | 'I' PI

<?TOKENS?>

CommentContentsE      ::= (                '('* [^:(]
                          | ':'+ ([^:()] | '('+ [^:(])
                          )*
                          ('('+ | ':'+ '('*)? & [(:]
CommentContents       ::= ( ':'+
                          | ':'* '('+
                          | ( ')'
                            | ':'* [^:()]
                            | ':'* '('+ [^:(]
                            )+
                            ':'* '('*
                          ) // & [(:]
PragmaContents        ::= ( ')' | '#'* [^#)] )*
                          '#'* & '#'
DirPIContents         ::= ( '>' | '?'* [^?>] )*
                          '?'* &'?'
CDataSectionContents  ::= (     ']'? [^#x5D]
                          | ']' ']'+ [^#x5D>]
                          )*
                          ']'* & ']]'
CDSect                ::= CDStart CData CDEnd
CDStart               ::= '<![CDATA['
CData                 ::= CDataSectionContents
CDEnd                 ::= ']]>'
CharData              ::= (     ']'? [^<&#x5D]
                          | ']' ']'+ [^<&#x5D>]
                          )*
                          ']'* & ']]'
PI                    ::= '<?' PITarget (S ( '>' | '?'* [^?>] )* '?'*)? '?>'
PITarget              ::= NameStartChar NameChar?
                        | (NameStartChar - ('X' | 'x'))  NameChar                 NameChar
                        |                  ('X' | 'x')  (NameChar - ('M' | 'm'))  NameChar
                        |                  ('X' | 'x')              ('M' | 'm')  (NameChar - ('L' | 'l'))
                        | NameStartChar NameChar NameChar NameChar+
NameStartChar         ::= ":" | [A-Z] | "_" | [a-z] | [#xC0-#xD6] | [#xD8-#xF6] | [#xF8-#x2FF] | [#x370-#x37D] | [#x37F-#x1FFF] | [#x200C-#x200D] | [#x2070-#x218F] | [#x2C00-#x2FEF] | [#x3001-#xD7FF] | [#xF900-#xFDCF] | [#xFDF0-#xFFFD] | [#x10000-#xEFFFF]
NameChar              ::= NameStartChar | "-" | "." | [0-9] | #xB7 | [#x0300-#x036F] | [#x203F-#x2040]
S                     ::= (#x20 | #x9 | #xD | #xA)+
